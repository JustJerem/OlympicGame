plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' version '1.7.0-1.0.6'
}

def versionNameDate = new Date().format("yyyy.M.d")
def versionCodeDate = new Date().format("yyyyMd").toInteger()
def versionBuild = 0

android {
    namespace 'com.jeremieguillot.olympicgame'
    compileSdk 33

    defaultConfig {
        applicationId "com.jeremieguillot.olympicgame"
        minSdk 24
        targetSdk 33
        versionCode versionCodeDate * 10 + versionBuild // 20204171
        versionName versionNameDate // 2020.4.17

        testInstrumentationRunner = "com.jeremieguillot.olympicgame.CustomTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    flavorDimensions "version"
    productFlavors{
        dev{
            dimension "version"
            buildConfigField "String", "API_BASE_URL", "\"https://ocs-test-backend.onrender.com/\""
        }
        staging{
            dimension "version"
            buildConfigField "String", "API_BASE_URL", "" //TODO need to be define when staging ready 05.06.2023
        }
        prod{
            dimension "version"
            buildConfigField "String", "API_BASE_URL", "" //TODO need to be define when prod ready 05.06.2023
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    sourceSets {
        test.resources.srcDirs += 'src/test/java/com/jeremieguillot/olympicgame/data/network/json'
    }
}

kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/devDebug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

dependencies {

    implementation 'com.google.android.material:material:1.9.0'
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'

    //Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material3:material3:1.2.0-alpha02'
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    //Hilt - Dependency injection
    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-compiler:2.42"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.1.0-alpha01'

    //Serialization - Moshi
    implementation("com.squareup.moshi:moshi-kotlin:1.14.0")
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    //Network Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.github.mrmike:ok2curl:0.7.0'

    //Nav
    implementation 'io.github.raamcosta.compose-destinations:animations-core:1.8.42-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.8.42-beta'

    //Log
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //Animation
    implementation "com.airbnb.android:lottie-compose:6.0.0"

    //Remote Image Compose
    implementation "io.coil-kt:coil-compose:2.4.0"

    //Markdown
    implementation "com.halilibo.compose-richtext:richtext-commonmark:0.16.0"

    //Tests
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'

    //UI Tests
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.42'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.42'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'

    //Debug
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}